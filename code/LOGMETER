LOGMETER:
REM Log multimeter data

REM Revision : 0.1
REM Author   : NoFitnessForPurpose
REM Date     : 23 Dec 2024
REM License  : MIT
REM Copyright: NoFitnessForPurpose 2025 All rights reserved

LOCAL a$(64) :REM File string
LOCAL data$(255) :REM serial data
LOCAL dtm$(32) :REM Hold log time
LOCAL lgfname$(20) :REM Name of log file
LOCAL lsamp$(255) :REM log o/p string
LOCAL i% :REM loop counter
LOCAL k% :REM Log loop counter

LOCAL ov,nv :REM old and new voltages

PRINT "    - LOG METER -   "
PRINT "Initialising"
ov = -999999
nv = 0.0
data$ = "None"
lsamp$ = ""
lgfname$ = "B:LOGFILE"
ONERR nocomms::
LSET:(9600,0,8,1,0,1,80,CHR$(13),-1,CHR$(13),-1,"",-1,-1)
nocomms::
IF (ERR > 0)
  AT 4,1 :PRINT "Error :";ERR$(ERR)
  GET
  STOP
ENDIF
ONERR OFF

REM Initialise meter
LPRINT "DS";CHR$(13)
data$ = LINPUT$:(2,2)

REM PRINT "DS :"; data$ :GET

REM Loging Loop
WHILE 1
  WHILE 1
    data$ = LINPUT$:(254,2)
    IF data$ = "" : BREAK : ENDIF
  ENDWH


  LPRINT "QM";CHR$(13);
  data$ = LINPUT$:(2,2)
  data$ = LINPUT$:(17,2)
  REM PRINT "QM :";data$ :GET
  dtm$ = DATIM$
  AT 1,1
  REM PRINT len(data$)
  PRINT "    - LOG METER -"
  PRINT RIGHT$(dtm$,20);
  PRINT RIGHT$(data$,12)

  IF LEFT$(data$,2) <> "QM"
    AT 1,4 : PRINT "Bad Msg.          ";
    PAUSE 20
    REM GET
    LPRINT "DS";CHR$(13);
    REM EMmpty buffer
    WHILE 1
      data$ = LINPUT$:(254,2)
      IF data$ = "" :BREAK :ENDIF
    ENDWH
  ELSE
    REM QM data$ was good
    REM Ignore conversio errors
    ONERR BADSTR::
    nv = VAL(MID$(data$,4,7))
    BADSTR::
    ONERR OFF
    REM If change or 30 minutes
    IF ((ov + 0.01) < nv) OR ((ov - 0.01) > nv) OR (k% > 295)
      REM Log to file
      lsamp$ = dtm$ + ", " + data$
      ONERR FEXIST::
        CREATE lgfname$,A,a$
        APPEND
        ERASE
        CLOSE
        AT 1,4 :PRINT "New log file created"
        PAUSE 20
      FEXIST::

      ONERR FERR::
        OPEN lgfname$,A,a$
        USE A
        APPEND
        A.a$ = lsamp$
        UPDATE
        CLOSE
     FERR::
     ONERR OFF
      ov = nv :REM Update old value
      BEEP 10,800
      k% = 0 :REM Reset counter
    ENDIF

  ENDIF

  i% = 1
  DO
    PAUSE 20
    AT 17,3 : PRINT "    " : AT 17,3  :PRINT i%
    i% = i% + 1
  UNTIL i% > 10
ENDWH
